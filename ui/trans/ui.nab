module ui/trans/ui

imports
	lib/nbl/-
	lib/task/-
	include/WebDSL
	ui/trans/signatures	
	function/trans/function
	
namespaces
	Page
	Template
	TemplateFunction

binding rules

	DefinePage(mods, name, fargs, targs, _):
		defines Page name
			of type PageSort(fargtypes)
		where fargs has type fargtypes
		scopes Variable, TemplateFunction

	DefineTemplate(mods, name, fargs, targs, _):
		defines Template name
			of type TemplateSort(fargtypes)
		where fargs has type fargtypes
		scopes Variable, TemplateFunction

	PageCall(name, args):
		refers to Page name

  discuss Guido Wachsmuth, Vlad Vergu, Danny Groenewegen --
    This is the current way to express conformance and best match.
  --
	TemplateCall(name, args, attrs, body):
		refers to best Template name of conformant type TemplateSort(argtypes)
		where args has type argtypes

	Action(_, name, fargs, block):
		defines TemplateFunction name
			of type FunctionSort(fargtypes, SimpleSort("Void"))
		  where fargs has type fargtypes
		scopes Variable

	ActionCall(name, args):
		refers to TemplateFunction name of type FunctionSort(argtypes, SimpleSort("Void"))
		where args has type argtypes

	ForAll(name, type, _, block, _):
		defines Variable name of type type in block

	For(name, type, _, _, block, _):
		defines Variable name of type type in block

	ForCount(name, _, _, block, _):
		defines Variable name of type SimpleSort("Int") in block

	ForInferred(name, expr, _, block, _):
		defines Variable name of type type in block
		where expr has type type
