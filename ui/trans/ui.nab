module ui/trans/ui

  author Danny Groenewegen
  author Vlad Vergu
  author Guido Wachsmuth
  
  version 0.1
  status under development
  
imports
	lib/nbl/-
	include/WebDSL
	function/trans/function
	
namespaces
	Page
	Template
	TemplateFunction

binding rules

	DefinePage(mods, name, fargs, targs, elems):
		defines Page name
			of type PageSort(fargtypes)
		where fargs has type fargtypes
		scopes Variable

	DefineTemplate(mods, name, fargs, targs, elems):
		defines Template name
			of type TemplateSort(fargtypes)
		where fargs has type fargtypes
		scopes Variable

	PageCall(name, args):
		refers to Page name

  discuss Guido Wachsmuth, Vlad Vergu, Danny Groenewegen --
    This is the current way to express conformance and best match.
  --
	TemplateCall(name, args, attrs, body):
		refers to best Template name of conformant type TemplateSort(argtypes)
		where args has type argtypes

	Action(_, name, fargs, block):
		defines TemplateFunction name
			of type FunctionSort(fargtypes, SimpleSort("Void"))
		  where fargs has type fargtypes
		scopes Variable

	ActionCall(name, args):
		refers to TemplateFunction name of type FunctionSort(argtypes, SimpleSort("Void"))
		where args has type argtypes

	discuss Guido Wachsmuth, Vlad Vergu --
    The current implementation disallows variable declarations inside template for loops.
    That is why no 'scopes Variable' is needed.
    
    This was stated in a comment before. 
    However, 'defines ... in block' introduces a scope for variables in block anyway.
	--
	ForAll(name, type, _, block, _):
		defines Variable name of type type in block

	For(name, type, _, _, block, _):
		defines Variable name of type type in block

	ForCount(name, _, _, block, _):
		defines Variable name of type SimpleSort("Int") in block

	ForInferred(name, expr, _, block, _):
		defines Variable name of type type in block
		where expr has type type
