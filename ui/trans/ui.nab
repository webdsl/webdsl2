module ui/trans/ui

imports
	lib/nbl/-
	lib/task/-
	include/WebDSL
	ui/trans/signatures	
	function/trans/function
	
namespaces
	Page
	Template
	TemplateFunction
	
properties
	signature of Page     : List(Type)
	signature of Template : List(Type)

binding rules

	DefinePage(mods, name, fargs, targs, _):
		scopes Variable, TemplateFunction
		defines Page name
				of signature fargs-ty
			where fargs has type fargs-ty

	DefineTemplate(mods, name, fargs, targs, _):
		scopes Variable, TemplateFunction
		defines Template name
				of signature fargs-ty
		  where fargs has type fargs-ty

	PageCall(name, args):
		refers to Page name
				of conformant signature args-ty
			where args has type args-ty

	TemplateCall(name, args, attrs, body):
		refers to best Template name 
				of conformant signature args-ty
			where args has type args-ty

	Action(_, name, fargs, block):
		defines TemplateFunction name
				of signature fargs-ty
				of type SimpleSort("Void")
		  where fargs has type fargs-ty
		scopes Variable

	ActionCall(name, args):
		refers to TemplateFunction name 
				of conformant signature args-ty
			where args has type args-ty

	ForAll(name, type, _, block, _):
		defines Variable name 
			of type type 
			in block

	For(name, type, _, _, block, _):
		defines Variable name 
			of type type 
			in block

	ForCount(name, _, _, block, _):
		defines Variable name 
			of type SimpleSort("Int") 
			in block

	ForInferred(name, expr, _, block, _):
		defines Variable name 
				of type type 
				in block
			where expr has type type
