module function/trans/function
 
imports
	include/WebDSL 
	function/trans/signatures
	datamodel/trans/datamodel
	function/trans/hql
	
namespaces
	Function
	ExtendedFunction
	Variable
	Type
	NativeClass
	NativeProperty
	TypeProperty
	
properties
	kind			of Function									: FunctionKind // Dynamic/Static/Global
	kind			of ExtendedFunction					: FunctionKind
	kind			of TypeFunction							: NativeFunctionKind
	signature of Function                 : List(Type)
	aliasing	of TypeFunction							: QId
	aliasing	of NativeClass							: NativeClassAlias
	parent		of NativeClass							: Id
	
binding rules // function declarations
	
	EntityFunction(Function(knd, name, args, ret-ty, _)):
		scopes Variable
		defines Function name
				of type ret-ty
				of signature args-ty
				of kind knd
			where args has type args-ty
	
	EntityFunction(ExtendFunction(Function(knd, name, args, ret-ty, _))):
		scopes Variable
		defines ExtendedFunction name 
				of type ret-ty
				of signature args-ty
				of kind knd
			where args has type args-ty
		refers to Function name
				of signature args-ty
				of kind knd
			where args has type args-ty
	
	GlobalFunction(Function(_, name, args, ret-ty, _)):
		scopes Variable
		defines Function name
				of type ret-ty
				of signature args-ty
				of kind Global()
			where args has type args-ty
	
	GlobalFunction(ExtendFunction(Function(_, name, args, ret-ty, _))):
		scopes Variable
		defines ExtendedFunction name
				of type ret-ty
				of signature args-ty
				of kind Global()
			where args has type args-ty
		refers to Function name
				of signature args-ty
				of kind Global()
			where args has type args-ty
	
	Arg(name, ty):
		defines Variable name of type ty
		
binding rules // function calls

	ThisCall(name, args):
		refers to best Function name
				of conformant signature args-ty
			where args has type args-ty
	
	Call(e, name, args):
		refers to best Function name 
				of conformant signature args-ty 
				in Entity t
			where e has type SimpleSort(t)
			where args has type args-ty
		otherwise
 		refers to best Function name 
				of conformant signature args-ty 
				in NativeClass t
			where e has type SimpleSort(t)
			where args has type args-ty
		otherwise
 		refers to best Function name 
				of conformant signature args-ty 
				in Type t
			where e has type SimpleSort(t)
			where args has type args-ty
		otherwise
		// Temporary workaround for GenericSort types.
		refers to best Function name 
				of conformant signature args-ty 
				in Entity t
			where e has type GenericSort(t, _)
			where args has type args-ty
		otherwise
 		refers to best Function name 
				of conformant signature args-ty 
				in NativeClass t
			where e has type GenericSort(t, _)
			where args has type args-ty
		otherwise
 		refers to best Function name 
				of conformant signature args-ty 
				in Type t
			where e has type GenericSort(t, _)
			where args has type args-ty
						
binding rules // expressions
	
	// sort reference (simple, generic, ref)
	SimpleSort(s):
		refers to Entity s
			otherwise
		refers to Enum s
			otherwise
		refers to Type s 
		  otherwise
		refers to NativeClass s
	
	// Not needed, RefSort contains a SimpleSort.
	/*RefSort(s):
		refers to Entity s
			otherwise
		refers to Enum s
			otherwise
		refers to Type s
	*/
	
	GenericSort(name, _):
		refers to Type name
	
	// var declaration * 3
	VarDecl(name, ty):
		defines Variable name of type ty in subsequent scope
	
	VarDeclInit(name, ty, _):
		defines Variable name of type ty in subsequent scope
	
	VarDeclInitInferred(name, expr):
		defines Variable name of type ty in subsequent scope
			where expr has type ty
		
	// var ref
	Var(name):
		refers to Variable name
			otherwise
		refers to Property name
			otherwise
		refers to ModuleVariable name
			otherwise
		refers to Entity name
			otherwise
		refers to Type name
			otherwise
		refers to NativeClass name

	// global var ref
	GlobalVar(name):
		refers to ModuleVariable name
	
	// scope escape var ref
	ExternalScopeVar(name):
		refers to Property name
			otherwise
		refers to ModuleVariable name
	
	Block(_):
		scopes Variable
	
	ForStmt(name, ty, _, _, block):
		defines Variable name of type ty in block
	
	ForStmtInferred(name, expr, _, block):
		defines Variable name of type ty in block
			where expr has type ty
	
	ForAllStmt(name, ty, _, block):
		defines Variable name of type ty in block
	
	ForCountStmt(name, _, _, block):
		defines Variable name of type SimpleSort("Int") in block
	
	ForExp(sexpr, name, ty, expr, filter):
		defines Variable name of type ty in /*sexpr,*/ expr, filter

	FieldAccess(e, member):
		refers to Property member in Entity e-ty
			where e has type SimpleSort(e-ty)
			otherwise
		refers to NativeProperty member in NativeClass e-ty
			where e has type SimpleSort(e-ty)
		refers to TypeProperty member in Type e-ty
			where e has type SimpleSort(e-ty)

binding rules // type declarations

	TypeDef(name, SimpleSort(super), _):
		defines non-unique Type name
		imports Function, TypeProperty from Type super
		scopes Function, TypeProperty
			//of parent "Object"
			
	TypeProperty(name, ty):
		defines TypeProperty name
			of type ty
	
	NativeClassFunction(knd, name, args-ty, r-ty):
		defines Function name
			of signature args-ty
			of type r-ty
			of kind knd
			of aliasing None()
	/*
	Rule for NativeClassFunctionFromStatic conflicts with rule above, 
	name receives double Def annotation when both are defined.
	Don't need a special rule for NativeClassFunctionFromStatic currently, 
	because the qname-fun alias is only needed for code generation.
	
	NativeClassFunctionFromStatic(qname-fun, NativeClassFunction(knd, name, args-ty, r-ty)):
		defines Function name
			of signature args-ty
			of type r-ty
			of kind knd
			of aliasing qname-fun
	*/
binding rules // native classes

	NativeClass(qname-class, NativeClassAlias(name), None(), _):
		scopes NativeProperty, Function
		defines NativeClass name
			of parent None()
			of aliasing qname-class
			of type SimpleSort(name)
			
	NativeClass(qname-class, NativeClassAlias(name), NativeClassSuper(par), _):
		imports NativeProperty, Function from NativeClass par
		scopes NativeProperty, Function
		defines NativeClass name
			of parent par
			of aliasing qname-class

	NativeClassProperty(member, ty):
		defines NativeProperty member
			of type ty


	
	// TODO: These should be handled in their respective sub-languages:
	 
	// validatetemplate
	// rendertemplate
	// sendemail, email, renderemail
	// goto page
	
	
	
	
	
	
	

