module function/trans/function

imports
	include/WebDSL

namespaces
	GlobalFunction
	ExtendedGlobalFunction
	EntityFunction
	ExtendedEntityFunction
	Variable
	
properties
	
	isstatic	of Function: Term

binding rules
	
	EntityFunction(Function(name, ps, _, _)):
		defines EntityFunction name
			of type (pts, name)
		where ps has type pts
		scopes Variable
	
	EntityFunction(ExtendedFunction(name, ps, _, _)):
		defines non-unique ExtendedEntityFunction name
			of type (pts, name)
		where ps has type pts
		scopes Variable
		refers to EntityFunction name
	
	StaticEntityFunction(name, ps, _, _):
		defines EntityFunction name
			of type (pts, name)
			of isstatic Static()
		where ps has type pts
		scopes Variable
	
	GlobalFunction(Function(name, ps, _, _)):
		defines GlobalFunction name
			of type (pts, name)
		where ps has type pts
		scopes Variable
	
	GlobalFunction(ExtendedFunction(name, ps, _, _)):
		defines non-unique ExtendedGlobalFunction name
			of type (pts, name)
		where ps has type pts
		scopes Variable
		refers to GlobalFunction name
	
	Arg(name, ty):
		defines Variable name of type ty
	
	
	// TODO: handle anonymous functions?
	
	
	