module datamodel/trans/datamodel

imports
	lib/nbl/-
	include/WebDSL

namespaces
	Entity
	ExtendedEntity
	Property
	Enum
	EnumValue
	ModuleVariable
	
properties

	parent						of Entity: ID
	ancestor					of Entity: List(ID)
	issession					of Entity: Term
	
	kind							of Property: Term
	isderived					of Property: Term
	annotations				of Property: List(Annotation)
	
binding rules

	Entity(name, par, _):
		defines Entity name
			of parent par
			of type SimpleSort(par)								
		scopes Property, EntityFunction, ExtendedEntityFunction
	
	ExtendEntity(name, _):
		defines non-unique ExtendedEntity name
		refers to Entity name // TODO do we want this reference ?
		scopes Property, Function, ExtendedFunction
		imports Property, EntityFunction, ExtendedEntityFunction from Entity name
		imports Property, EntityFunction, ExtendedEntityFunction into Entity name
	
	SessionEntity(name, _):
		defines Entity name
			of type SimpleSort(par)
		defines Variable name
			of type SimpleSort(name)
			of issession SessionEntity()
		scopes Property, EntityFunction, ExtendedEntityFunction

	// constraint: SimpleSort(name) is a session entity	
	ExtendSessionEntity(name, _):
		defines non-unique ExtendedEntity name
		refers to Entity name of issession SessionEntity() // TODO: should we enforce this issession here?
		scopes Property, Function, ExtendedFunction
		imports Property, Function, ExtendedFunction from Entity name
		imports Property, Function, ExtendedFunction into Entity name

	Property(name, knd, ty, annos):
		defines Property name
			of type ty
			of kind knd
			of annotations annos // TODO: should we just store the entire AST for property definition?
	
	DerivedProperty(name, knd, ty, annos, _):
		defines Property name
			of type ty
			of kind knd
			of annotations annos // TODO: should we just store the entire AST for property definition?
			of isderived Derived()
	
	Enum(name, _):
		defines Enum name
		scopes EnumValue
	
	EnumValue(name, _):
		defines EnumValue name

		// var declaration * 3
	GlobalVarDecl(name, ty):
		defines ModuleVariable name of type ty
	
	GlobalVarDeclInit(name, ty, _):
		defines ModuleVariable name of type ty
	
	GlobalVarDeclInitInferred(name, expr):
		defines ModuleVariable name of type ty
		where expr has type ty

binding rules // annotation

	InverseAnno(parent, member):
		refers to Property member in Entity parent

