module datamodel/trans/datamodel

  author Danny Groenewegen
  author Vlad Vergu
  
  version 0.1
  status under development
  
imports
	lib/nbl/-
	lib/task/-
	include/WebDSL
	function/trans/function
	datamodel/trans/signatures

namespaces
	Entity
	ExtendedEntity
	Property
	Enum
	EnumValue
	ModuleVariable
	
properties

	parent						of Entity: ID
	ancestor					of Entity: List(ID)
	kind							of Entity: EntityKind
	
	propkind					of Property: Term
	derived						of Property: BooleanLit
	annotations				of Property: List(Annotation)
	
binding rules

	Entity(knd, name, par, _):
		defines Entity name
			of parent par
			of kind knd
		scopes Property, EntityFunction, ExtendedEntityFunction
		imports Property, EntityFunction, ExtendedEntityFunction from ExtendedEntity name
		
	todo -- generate import-site --
	ExtendEntity(knd, name, _):
		scopes Property, EntityFunction, ExtendedEntityFunction
		defines non-unique ExtendedEntity name of kind knd
		refers to Entity name of kind knd
		imports Property, EntityFunction, ExtendedEntityFunction from Entity name

	Property(name, knd, ty, annos):
		defines Property name
			of type ty
			of propkind knd
			of annotations annos
			of derived False()
				
	DerivedProperty(name, knd, ty, annos, expr):
		defines Property name
			of type ty
			of propkind knd
			of annotations annos
			of derived True()
	
	Enum(name, _):
		scopes EnumValue
		defines Enum name
	
	EnumValue(name, _):
		defines EnumValue name

		// var declaration * 3
	GlobalVarDecl(name, ty):
		defines ModuleVariable name of type ty
	
	GlobalVarDeclInit(name, ty, _):
		defines ModuleVariable name of type ty
	
	GlobalVarDeclInitInferred(name, expr):
		defines ModuleVariable name of type ty
			where expr has type ty

binding rules // annotation

	InverseAnno(par, member):
		refers to Property member in Entity par

