module datamodel/trans/datamodel

imports
	include/WebDSL
	function/trans/function
	datamodel/trans/signatures

namespaces
	Entity
	ExtendedEntity
	Property
	Enum
	EnumValue
	ModuleVariable
	
properties

	parent						of Entity: ID
	ancestor					of Entity: List(ID)
	kind							of Entity: EntityKind
	
	propkind					of Property: Term
	derived						of Property: BooleanLit
	annotations				of Property: List(Annotation)
	
binding rules
	
	Entity(knd, name, fakename, SimpleSort(par), _):
		defines Entity name
			of parent par
			of kind knd
			of type SimpleSort(name)
		imports Property, imported Property, Function, imported Function from Entity par
		scopes Property, Function, ExtendedFunction, Variable
		imports Property, Function, ExtendedFunction from ExtendedEntity fakename
		//implicitly defines Variable "this" of type SimpleSort(name)
		
	ExtendEntity(knd, name, fakename, _):
		defines non-unique ExtendedEntity fakename of kind knd
		scopes Property, Function, ExtendedFunction, Variable
		imports Property, Function, ExtendedFunction from Entity name of kind knd

	Property(name, knd, ty, annos):
		defines Property name
			of type ty
			of propkind knd
			of annotations annos
			of derived False()
				
	DerivedProperty(name, knd, ty, annos, expr):
		defines Property name
			of type ty
			of propkind knd
			of annotations annos
			of derived True()
	
	Enum(name, _):
		scopes EnumValue
		defines Enum name
			of type SimpleSort(name)
	
	EnumValue(name, _):
		defines EnumValue name

		// var declaration * 3
	GlobalVarDecl(name, ty):
		defines ModuleVariable name of type ty
	
	GlobalVarDeclInit(name, ty, _):
		defines ModuleVariable name of type ty
	
	GlobalVarDeclInitInferred(name, expr):
		defines ModuleVariable name of type ty
      where expr has type ty 
	
binding rules // annotation

	InverseAnno(par, member):
		refers to Property member in Entity par

